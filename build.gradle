plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'java'
}

group = 'com.github.magiccheese1.damageindicator'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}


dependencies {
    implementation 'com.tchristofferson:ConfigUpdater:1.2-SNAPSHOT'
    compileOnly 'org.jetbrains:annotations:22.0.0'
    compileOnly 'io.papermc.paper:paper:1.17.1-R0.1-SNAPSHOT'

    testImplementation 'junit:junit:4.13'
    testImplementation 'io.papermc.paper:paper:1.17.1-R0.1-SNAPSHOT'
}

test {
    useJUnit()
}

shadowJar {
    relocate 'com.tchristofferson.ConfigUpdater', 'io.github.magiccheese1.damageindicator.ConfigUpdater'
}
jar.dependsOn(shadowJar)

def targetJavaVersion = 11
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

//Replace plugin name and version in plugin.yml
processResources {
    def props = [version: version, group: project.getGroup()]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
